app OpenSaaS {
  wasp: {
    version: "^0.19.0"
  },

  title: "Loom - The Somatic Coaching Platform",

  head: [
    "<link rel='icon' href='/favicon.ico' />",
    "<meta charset='utf-8' />",
    "<meta name='description' content='The all-in-one workspace for Satya Method practitioners. Visualise client sensations, track session history, and manage your practice effortlessly.' />",
    "<meta name='author' content='Loom Platform' />",
    "<meta name='keywords' content='somatic coaching, Satya Method, body mapping, sensation tracking, somatic therapy' />",

    "<meta property='og:type' content='website' />",
    "<meta property='og:title' content='Loom - The Somatic Coaching Platform' />",
    "<meta property='og:site_name' content='Loom' />",
    "<meta property='og:url' content='https://loom-platform.com' />",
    "<meta property='og:description' content='The all-in-one workspace for Satya Method practitioners. Visualise client sensations, track session history, and manage your practice effortlessly.' />",
    "<meta property='og:image' content='https://loom-platform.com/public-banner.webp' />",
    "<meta name='twitter:image' content='https://loom-platform.com/public-banner.webp' />",
    "<meta name='twitter:image:width' content='800' />",
    "<meta name='twitter:image:height' content='400' />",
    "<meta name='twitter:card' content='summary_large_image' />",
    "<script defer data-domain='<your-site-id>' src='https://plausible.io/js/script.js'></script>",
    "<script defer data-domain='<your-site-id>' src='https://plausible.io/js/script.local.js'></script>",
  ],

  // üîê Auth out of the box! https://wasp.sh/docs/auth/overview
  auth: {
    userEntity: User,
    methods: {
      // NOTE: If you decide to not use email auth, make sure to also delete the related routes and pages below.
      //   (RequestPasswordReset(Route|Page), PasswordReset(Route|Page), EmailVerification(Route|Page))
      email: {
        fromField: {
          name: "Loomplatform",
          email: "loomcoachingplatform@gmail.com"
        },
        emailVerification: {
          clientRoute: EmailVerificationRoute,
          getEmailContentFn: import { getVerificationEmailContent } from "@src/auth/email-and-pass/emails",
        },
        passwordReset: {
          clientRoute: PasswordResetRoute,
          getEmailContentFn: import { getPasswordResetEmailContent } from "@src/auth/email-and-pass/emails",
        },
        userSignupFields: import { getEmailUserFields } from "@src/auth/userSignupFields",
      },
      // Uncomment to enable Google Auth (check https://wasp.sh/docs/auth/social-auth/google for setup instructions):
      google: { // Guide for setting up Auth via Google
         userSignupFields: import { getGoogleUserFields } from "@src/auth/userSignupFields",
         configFn: import { getGoogleAuthConfig } from "@src/auth/userSignupFields",
       },
      // Uncomment to enable GitHub Auth (check https://wasp.sh/docs/auth/social-auth/github for setup instructions):
      // gitHub: {
      //   userSignupFields: import { getGitHubUserFields } from "@src/auth/userSignupFields",
      //   configFn: import { getGitHubAuthConfig } from "@src/auth/userSignupFields",
      // },
      // Uncomment to enable Discord Auth (check https://wasp.sh/docs/auth/social-auth/discord for setup instructions):
      // discord: {
      //   userSignupFields: import { getDiscordUserFields } from "@src/auth/userSignupFields",
      //   configFn: import { getDiscordAuthConfig } from "@src/auth/userSignupFields"
      // }
    },
    onAuthFailedRedirectTo: "/login",
    onAuthSucceededRedirectTo: "/coach",
  },

  db: {
    // Run `wasp db seed` to seed the database with the seed functions below:
    seeds: [
      // Populates the database with a bunch of fake users to work with during development.
      import { seedMockUsers } from "@src/server/scripts/dbSeeds",
      // Creates missing CoachProfile/ClientProfile for existing users
      import { createMissingProfiles } from "@src/server/scripts/createMissingProfiles",
      // Creates test coach and clients with realistic sample data for development
      import { seedTestCoachWithClients } from "@src/server/scripts/dbSeeds",
    ]
  },

  client: {
    rootComponent: import App from "@src/client/App",
  },

  emailSender: {
    // NOTE: "Dummy" provider is just for local development purposes.
    //   Make sure to check the server logs for the email confirmation url (it will not be sent to an address)!
    //   Once you are ready for production, switch to e.g. "SendGrid" or "Mailgun" providers. Check out https://docs.opensaas.sh/guides/email-sending/ .
    provider: SendGrid,
    defaultFrom: {
      name: "Loomplatform",
      // When using a real provider, e.g. SendGrid, you must use the same email address that you configured your account to send out emails with!
      email: "loomcoachingplatform@gmail.com"
    },
  },
}

route LandingPageRoute { path: "/", to: LandingPage }
page LandingPage {
  component: import LandingPage from "@src/landing-page/LandingPage"
}

//#region Auth Pages
route LoginRoute { path: "/login", to: LoginPage }
page LoginPage {
  component: import Login from "@src/auth/LoginPage"
}

route SignupRoute { path: "/signup", to: SignupPage }
page SignupPage {
  component: import { Signup } from "@src/auth/SignupPage"
}

route RequestPasswordResetRoute { path: "/request-password-reset", to: RequestPasswordResetPage }
page RequestPasswordResetPage {
  component: import { RequestPasswordResetPage } from "@src/auth/email-and-pass/RequestPasswordResetPage",
}

route PasswordResetRoute { path: "/password-reset", to: PasswordResetPage }
page PasswordResetPage {
  component: import { PasswordResetPage } from "@src/auth/email-and-pass/PasswordResetPage",
}

route EmailVerificationRoute { path: "/email-verification", to: EmailVerificationPage }
page EmailVerificationPage {
  component: import { EmailVerificationPage } from "@src/auth/email-and-pass/EmailVerificationPage",
}
//#endregion

//#region User & Dashboards
route AccountRoute { path: "/account", to: AccountPage }
page AccountPage {
  authRequired: true,
  component: import Account from "@src/user/AccountPage"
}

route CoachDashboardRoute { path: "/coach", to: CoachDashboardPage }
page CoachDashboardPage {
  authRequired: true,
  component: import CoachDashboard from "@src/coach/CoachDashboardPage"
}

query getClientsForCoach {
  fn: import { getClientsForCoach } from "@src/coach/operations",
  entities: [User, CoachProfile, ClientProfile, SomaticLog, CoachSession]
}

route ClientDetailsRoute { path: "/coach/client/:clientId", to: ClientDetailsPage }
page ClientDetailsPage {
  authRequired: true,
  component: import ClientDetails from "@src/coach/ClientDetailsPage"
}

route ClientDashboardRoute { path: "/client", to: ClientDashboardPage }
page ClientDashboardPage {
  authRequired: true,
  component: import ClientDashboard from "@src/client/ClientDashboardPage"
}

query getPaginatedUsers {
  fn: import { getPaginatedUsers } from "@src/user/operations",
  entities: [User]
}

action updateIsUserAdminById {
  fn: import { updateIsUserAdminById } from "@src/user/operations",
  entities: [User]
}

action updateUserLanguage {
  fn: import { updateUserLanguage } from "@src/user/operations",
  entities: [User]
}
//#endregion

//#region Invitations
action inviteClient {
  fn: import { inviteClient } from "@src/invitation/operations",
  entities: [User, CoachProfile, ClientInvitation]
}

action acceptInvitation {
  fn: import { acceptInvitation } from "@src/invitation/operations",
  entities: [User, ClientProfile, ClientInvitation, CoachProfile]
}

query getPendingInvitations {
  fn: import { getPendingInvitations } from "@src/invitation/operations",
  entities: [CoachProfile, ClientInvitation]
}

route AcceptInviteRoute { path: "/accept-invite", to: AcceptInvitePage }
page AcceptInvitePage {
  component: import AcceptInvite from "@src/invitation/AcceptInvitePage"
}
//#endregion

//#region Somatic Logs
action createSomaticLog {
  fn: import { createSomaticLog } from "@src/somatic-logs/operations",
  entities: [User, ClientProfile, SomaticLog]
}

query getSomaticLogs {
  fn: import { getSomaticLogs } from "@src/somatic-logs/operations",
  entities: [User, ClientProfile, CoachProfile, SomaticLog]
}
//#endregion

//#region Sessions
action createSession {
  fn: import { createSession } from "@src/session/operations",
  entities: [User, CoachProfile, ClientProfile, CoachSession]
}

action updateSession {
  fn: import { updateSession } from "@src/session/operations",
  entities: [User, CoachProfile, CoachSession]
}

action deleteSession {
  fn: import { deleteSession } from "@src/session/operations",
  entities: [User, CoachProfile, CoachSession]
}

query getSessionsForClient {
  fn: import { getSessionsForClient } from "@src/session/operations",
  entities: [User, ClientProfile, CoachProfile, CoachSession]
}

query getRecentSessionsForClient {
  fn: import { getRecentSessionsForClient } from "@src/session/operations",
  entities: [User, ClientProfile, CoachProfile, CoachSession]
}

query getSessionContext {
  fn: import { getSessionContext } from "@src/session/operations",
  entities: [User, CoachProfile, ClientProfile, SomaticLog]
}

//#region Module 10: Recurring Sessions
action updateClientSchedule {
  fn: import { updateClientSchedule } from "@src/session/operations",
  entities: [User, CoachProfile, ClientProfile]
}

action logSession {
  fn: import { logSession } from "@src/session/operations",
  entities: [User, CoachProfile, ClientProfile, CoachSession]
}

query getUpcomingSessions {
  fn: import { getUpcomingSessions } from "@src/session/operations",
  entities: [User, CoachProfile, ClientProfile]
}

route LogSessionRoute { path: "/coach/client/:clientId/log-session", to: LogSessionPage }
page LogSessionPage {
  authRequired: true,
  component: import LogSession from "@src/coach/LogSessionPage"
}

route SessionHistoryRoute { path: "/client/sessions", to: SessionHistoryPage }
page SessionHistoryPage {
  authRequired: true,
  component: import SessionHistory from "@src/client/SessionHistoryPage"
}
//#endregion Module 10

//#region Module 11: Goals & Milestones
action createGoal {
  fn: import { createGoal } from "@src/goals/operations",
  entities: [User, ClientProfile, Goal, Milestone]
}

action updateGoal {
  fn: import { updateGoal } from "@src/goals/operations",
  entities: [User, ClientProfile, CoachProfile, Goal, Milestone]
}

action deleteGoal {
  fn: import { deleteGoal } from "@src/goals/operations",
  entities: [User, ClientProfile, CoachProfile, Goal, Milestone]
}

query getGoals {
  fn: import { getGoals } from "@src/goals/operations",
  entities: [User, ClientProfile, CoachProfile, Goal, Milestone]
}

action toggleMilestone {
  fn: import { toggleMilestone } from "@src/goals/operations",
  entities: [User, ClientProfile, CoachProfile, Goal, Milestone]
}

action updateGoalProgress {
  fn: import { updateGoalProgress } from "@src/goals/operations",
  entities: [User, ClientProfile, CoachProfile, Goal, Milestone]
}

route ClientGoalsRoute { path: "/client/goals", to: ClientGoalsPage }
page ClientGoalsPage {
  authRequired: true,
  component: import ClientGoals from "@src/client/GoalsPage"
}

route CoachClientGoalsRoute { path: "/coach/client/:clientId/goals", to: CoachClientGoalsPage }
page CoachClientGoalsPage {
  authRequired: true,
  component: import CoachClientGoals from "@src/coach/ClientGoalsPage"
}
//#endregion Module 11
//#endregion

//#region Payment
route PricingPageRoute { path: "/pricing", to: PricingPage }
page PricingPage {
  component: import PricingPage from "@src/payment/PricingPage"
}

route CheckoutResultRoute { path: "/checkout", to: CheckoutResultPage }
page CheckoutResultPage {
  authRequired: true,
  component: import CheckoutResultPage from "@src/payment/CheckoutResultPage"
}

query getCustomerPortalUrl {
  fn: import { getCustomerPortalUrl } from  "@src/payment/operations",
  entities: [User]
}

action generateCheckoutSession {
  fn: import { generateCheckoutSession } from "@src/payment/operations",
  entities: [User]
}

api paymentsWebhook {
  fn: import { paymentsWebhook } from "@src/payment/webhook",
  entities: [User, TranzillaTransaction],
  middlewareConfigFn: import { paymentsMiddlewareConfigFn } from "@src/payment/webhook",
  httpRoute: (POST, "/payments-webhook")
}
//#endregion

//#region File Upload
route FileUploadRoute { path: "/file-upload", to: FileUploadPage }
page FileUploadPage {
  authRequired: true,
  component: import FileUpload from "@src/file-upload/FileUploadPage"
}

action createFileUploadUrl {
  fn: import { createFileUploadUrl } from "@src/file-upload/operations",
  entities: [User, File]
}

action addFileToDb {
  fn: import { addFileToDb } from "@src/file-upload/operations",
  entities: [User, File]
}

query getAllFilesByUser {
  fn: import { getAllFilesByUser } from "@src/file-upload/operations",
  entities: [User, File]
}

query getDownloadFileSignedURL {
  fn: import { getDownloadFileSignedURL } from "@src/file-upload/operations",
  entities: [User, File]
}

action deleteFile {
  fn: import { deleteFile } from "@src/file-upload/operations",
  entities: [User, File]
}
//#endregion

//#region Resource Library (Module 8)
route CoachResourcesRoute { path: "/coach/resources", to: CoachResourcesPage }
page CoachResourcesPage {
  authRequired: true,
  component: import CoachResources from "@src/coach/ResourcesPage"
}

route ClientResourcesRoute { path: "/client/resources", to: ClientResourcesPage }
page ClientResourcesPage {
  authRequired: true,
  component: import ClientResources from "@src/client/ResourcesPage"
}

action getUploadUrl {
  fn: import { getUploadUrl } from "@src/resources/operations",
  entities: [User, CoachProfile, Resource]
}

action createResource {
  fn: import { createResource } from "@src/resources/operations",
  entities: [User, CoachProfile, Resource]
}

query getCoachResources {
  fn: import { getCoachResources } from "@src/resources/operations",
  entities: [User, CoachProfile, ClientProfile, Resource]
}

query getResourceDownloadUrl {
  fn: import { getResourceDownloadUrl } from "@src/resources/operations",
  entities: [User, CoachProfile, ClientProfile, Resource]
}

action deleteResource {
  fn: import { deleteResource } from "@src/resources/operations",
  entities: [User, CoachProfile, Resource]
}
//#endregion

//#region Analytics
query getDailyStats {
  fn: import { getDailyStats } from "@src/analytics/operations",
  entities: [User, DailyStats]
}

job dailyStatsJob {
  executor: PgBoss,
  perform: {
    fn: import { calculateDailyStats } from "@src/analytics/stats"
  },
  schedule: {
    cron: "0 * * * *" // every hour. useful in production
    // cron: "* * * * *" // every minute. useful for debugging
  },
  entities: [User, DailyStats, Logs, PageViewSource]
}

job checkExpiredSubscriptionsJob {
  executor: PgBoss,
  perform: {
    fn: import { checkExpiredSubscriptions } from "@src/payment/worker"
  },
  schedule: {
    cron: "0 2 * * *" // Daily at 2 AM
  },
  entities: [User]
}
//#endregion

//#region Admin Dashboard
route AdminRoute { path: "/admin", to: AnalyticsDashboardPage }
page AnalyticsDashboardPage {
  authRequired: true,
  component: import AnalyticsDashboardPage from "@src/admin/dashboards/analytics/AnalyticsDashboardPage"
}

route AdminUsersRoute { path: "/admin/users", to: AdminUsersPage }
page AdminUsersPage {
  authRequired: true,
  component: import AdminUsers from "@src/admin/dashboards/users/UsersDashboardPage"
}

route AdminSettingsRoute { path: "/admin/settings", to: AdminSettingsPage }
page AdminSettingsPage {
  authRequired: true,
  component: import AdminSettings from "@src/admin/elements/settings/SettingsPage"
}

route AdminCalendarRoute { path: "/admin/calendar", to: AdminCalendarPage }
page AdminCalendarPage {
  authRequired: true,
  component: import AdminCalendar from "@src/admin/elements/calendar/CalendarPage"
}


route AdminUIButtonsRoute { path: "/admin/ui/buttons", to: AdminUIButtonsPage }
page AdminUIButtonsPage {
  authRequired: true,
  component: import AdminUI from "@src/admin/elements/ui-elements/ButtonsPage"
}

//#region Demo Pages (for screenshots)
route DemoBodyMapRoute { path: "/demo-body-map", to: DemoBodyMapPage }
page DemoBodyMapPage {
  component: import BodyMapPromoDemo from "@src/landing-page/BodyMapPromoDemoPage"
}
//#endregion

route NotFoundRoute { path: "*", to: NotFoundPage }
page NotFoundPage {
  component: import { NotFoundPage } from "@src/client/components/NotFoundPage"
}

//#region Contact Form Messages
// TODO:
// add functionality to allow users to send messages to admin
// and make them accessible via the admin dashboard
route AdminMessagesRoute { path: "/admin/messages", to: AdminMessagesPage }
page AdminMessagesPage {
  authRequired: true,
  component: import AdminMessages from "@src/admin/dashboards/messages/MessagesPage"
}
//#endregion
//#endregion
